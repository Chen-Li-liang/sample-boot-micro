buildscript {
  ext {
    springCloudVersion = 'Dalston.SR1'
    springBootVersion = '1.5.3.RELEASE'
  }
  ext["hibernate.version"] = "5.2.10.Final" 
  repositories {
    jcenter()
  }
  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
  }
}

allprojects {
  version = "1.5.0"
}

subprojects {
  apply plugin: "org.springframework.boot"

  sourceCompatibility = '1.8'
  targetCompatibility = '1.8'

  def defaultEncoding = 'UTF-8'
  tasks.withType(AbstractCompile).each { it.options.encoding = defaultEncoding }

  javadoc {
    options.encoding = defaultEncoding
    options.addBooleanOption('Xdoclint:none', true)
  }

  test {
    include "**/model/**", "**/controller/**", "**/usecase/**", "**/util/**", "**/context/**"
  }

  springBoot {
    executable = true
    buildInfo()
  }

  repositories {
    jcenter()
  }

  dependencyManagement {
    imports {
      mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
      mavenBom "org.springframework.boot:spring-boot-starter-parent:${springBootVersion}"
    }
  }
    
  dependencies {
    compileOnly "org.projectlombok:lombok:1.16.16"
    compile "org.springframework.cloud:spring-cloud-starter-eureka"
    compile "org.springframework.cloud:spring-cloud-starter-feign"
    compile "org.springframework.cloud:spring-cloud-starter-sleuth"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-security"
    compile "org.springframework.boot:spring-boot-starter-aop"
    compile "org.springframework.boot:spring-boot-starter-cache"
    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.ehcache:ehcache:3.2.+"
    compile "javax.cache:cache-api:1.0.0"
    compile "com.zaxxer:HikariCP:2.6.+"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-hibernate5:2.8.+"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.8.+"
    compile "org.eclipse.collections:eclipse-collections:8.1.0"
    compile "org.eclipse.collections:eclipse-collections-api:8.1.0"
    compile "commons-io:commons-io:2.5"
    compile "org.apache.commons:commons-lang3:3.5"
    compile "com.ibm.icu:icu4j:59.1"
    compile "org.jolokia:jolokia-core:1.3.6"
    compile fileTree(dir: 'libs', includes: ['*.jar'])
    runtime "com.h2database:h2:1.4.195"
    testCompileOnly "org.projectlombok:lombok:1.16.16"
    testCompile "org.springframework.boot:spring-boot-starter-test"
  }
}

project(':micro-app') {
  dependencies {
    compile project(':micro-core')
  }
}

project(':micro-asset') {
  dependencies {
    compile project(':micro-core')
  }
}

project(':micro-web') {
  dependencies {
    compile project(':micro-core')
    compile project(':micro-asset')
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = "3.5"
}
