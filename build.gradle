buildscript {
  ext {
    springCloudVersion = 'Brixton.SR4'
    springBootVersion = '1.4.0.RC1'
  }
  repositories {
    jcenter()
    maven { url "http://repo.spring.io/snapshot" }
    maven { url "http://repo.spring.io/milestone" }
  }
  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
  }
}

allprojects {
  version = "1.0.0"
}

subprojects {
  apply plugin: "spring-boot"

  sourceCompatibility = '1.8'
  targetCompatibility = '1.8'

  def defaultEncoding = 'UTF-8'
  tasks.withType(AbstractCompile).each { it.options.encoding = defaultEncoding }

  javadoc {
    options.encoding = defaultEncoding
    options.addBooleanOption('Xdoclint:none', true)
  }


  test {
    include "**/model/**", "**/controller/**", "**/usecase/**", "**/util/**", "**/context/**"
  }

  springBoot {
    executable = true
  }

  repositories {
    jcenter()
    maven { url "http://repo.spring.io/snapshot" }
    maven { url "http://repo.spring.io/milestone" }
  }

  dependencyManagement {
    imports {
      mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
      mavenBom "org.springframework.boot:spring-boot-starter-parent:${springBootVersion}"
    }
  }
    
  dependencies {
    compileOnly "org.projectlombok:lombok:1.16.8"
    compile "org.springframework.cloud:spring-cloud-starter-eureka"
    compile "org.springframework.cloud:spring-cloud-starter-feign"
    compile "org.springframework.cloud:spring-cloud-starter-sleuth"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-security"
    compile "org.springframework.boot:spring-boot-starter-aop"
    compile "org.springframework.boot:spring-boot-starter-cache"
    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.hibernate:hibernate-java8"
    compile "org.ehcache:ehcache:3.1.+"
    compile "javax.cache:cache-api:1.0.0"
    compile "com.zaxxer:HikariCP:2.4.+"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-hibernate5:2.8.+"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.8.+"
    compile "commons-io:commons-io:2.5"
    compile "org.apache.commons:commons-collections4:4.1"
    compile "org.apache.commons:commons-lang3:3.4"
    compile "com.ibm.icu:icu4j:57.1"
    compile "org.jolokia:jolokia-core:1.3.3"
    compile fileTree(dir: 'libs', includes: ['*.jar'])
    runtime "com.h2database:h2:1.4.+"
    testCompileOnly "org.projectlombok:lombok:1.16.8"
    testCompile "org.springframework.boot:spring-boot-starter-test"
  }
}

project(':micro-app') {
    dependencies {
        compile project(':micro-core')
    }
}

project(':micro-asset') {
    dependencies {
        compile project(':micro-core')
    }
}

project(':micro-web') {
    dependencies {
        compile project(':micro-core')
        compile project(':micro-asset')
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "2.14.1"
}
